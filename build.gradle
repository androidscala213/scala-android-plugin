buildscript {
    ext {
        androidVersion = '4.0.0'
    }
}

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.5"
}

repositories {
    google()
    jcenter()
}

group = 'scala.android.plugin'
version = new Date().format('yyyyMMdd.HHmm')

dependencies {
    implementation "com.android.tools.build:gradle:$androidVersion"
    // Use JUnit test framework for unit tests
    testImplementation('org.codehaus.groovy:groovy-all:2.5.7')
    testImplementation('org.spockframework:spock-core:1.3-groovy-2.5')

    testImplementation 'junit:junit:4.13'
}

gradlePlugin {
    // Define the plugin
    plugins {
        scalaAndroidPlugin {
            id = 'scala.android'
            implementationClass = 'scala.android.plugin.ScalaAndroidPlugin'
        }
    }
}

// Add a source set for the functional test suite
sourceSets {
    functionalTest {
        groovy.srcDir file('src/functionalTest/groovy')
        resources.srcDir file('src/functionalTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

// Add a task to run the functional tests
tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = group
            artifactId = 'scala-android-plugin'

            pom {
                name = "Scala android plugin"
                description = "Scala android plugin"
                url = "https://github.com/dsvdsv/scala-android-plugin"
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'scala-android-plugin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/dsvdsv/scala-android-plugin'
        version {
            name = project.version
        }
    }
}
